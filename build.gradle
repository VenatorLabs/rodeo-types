import java.nio.file.Files
import java.nio.file.Paths
import java.nio.file.StandardCopyOption

plugins {
    id 'java-library'
    id 'checkstyle'
    id 'com.github.spotbugs' version '6.0.6'
}

// current version of the project
version '0.0.0'
group 'org.venatorlabs.rodeo'

ext {
    // Global project variables accessible from project.<VARIABLENAME>
    checkstyleVersion = '10.7.0'
    checkstyleCfgProjectPath = 'config/checkstyle/checkstyle.xml'
    spotbugsExcludeCfgProjectPath = 'config/spotbugs/excludeFilter.xml'
    rootDistDir = "${project.rootDir}/dist"
}

repositories {
    mavenCentral()
}

configurations {
    compileClasspath.extendsFrom(annotationProcessor)
}

dependencies {
    annotationProcessor 'com.github.spotbugs:spotbugs-annotations:4.7.3'
}

checkstyle {
    println "configuring $project.archivesBaseName checkstyle@$project.checkstyleVersion"
    toolVersion project.checkstyleVersion
    // get back to project root & then to central configuration
    configFile file(project.checkstyleCfgProjectPath)
}

checkstyleMain {
    doFirst {
        println "performing checkstyle on main directory in $project.archivesBaseName"
        source = 'src/main/java/org'
    }
}

compileJava {
    doFirst {
        println "compiling ${project.name}"
    }
    sourceCompatibility '11'
    targetCompatibility '11'
}

java {
    withSourcesJar()
    withJavadocJar()
}

sourcesJar {
    doFirst {
        println "creating ${sourcesJar.archiveFile.getAsFile().get().getName()}"
    }
    doLast {
        println "created sources @ ${sourcesJar.archiveFile.getAsFile().get()}"
    }
}

javadocJar {
    doFirst {
        println "creating ${javadocJar.archiveFile.getAsFile().get().getName()}"
    }
    doLast {
        println "created javadoc @ ${javadocJar.archiveFile.getAsFile().get()}"
    }
}

jar {
    ext {
        if (project.hasProperty("rodeoBuild")) {
            libName = "types-lib"
        } else {
            libName = rootProject.name
        }
        projPath = "${project.group}.types".replaceAll("\\.", "/")
    }
    archivesBaseName = "$libName"
    archiveClassifier = null
    metaInf {
        into (projPath) {
            if (project.hasProperty("rodeoBuild")) {
                from project.file("LICENSE")
            } else {
                from rootProject.file("LICENSE")
            }
        }
    }
    doFirst {
        println "creating ${jar.archiveFile.getAsFile().get().getName()}"
    }
    doLast {
        println "created library @ ${jar.archiveFile.getAsFile().get()}"

        // Lift up distributable into top level distribution folder
        java.io.File outputJar = jar.archiveFile.getAsFile().get()
        java.nio.file.Path distFilePath = Paths.get(project.rootDistDir, "${rootProject.name}-types-${project.version}.jar")
        java.io.File distFile = distFilePath.toFile()
        distFile.getParentFile().mkdirs()
        Files.copy(
            outputJar.toPath(),
            distFile.toPath(),
            StandardCopyOption.REPLACE_EXISTING
        )
        println "created distribution jar @ $distFilePath"
    }
}

spotbugs {
    excludeFilter = file(project.spotbugsExcludeCfgProjectPath)
}

spotbugsMain {
    doFirst {
        println "verifying code with spotbugs"
    }
}
